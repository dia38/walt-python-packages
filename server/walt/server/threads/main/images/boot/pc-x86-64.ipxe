#!ipxe

set node_model pc-x86-64
set walt_init /bin/walt-init

# compute kernel url
set kernel_url /nodes/${mac:hexhyp}/${node_model}/kernel
set initrd_url /nodes/${mac:hexhyp}/${node_model}/initrd

# compute kernel command line args
set nfs_root ${next-server}:/var/lib/walt/nodes/${mac}

# Specifying the NFS version is necessary for initrd-less images: the
# kernel defaults to NFS version 2 and somehow panics even before
# running init. Images with initrd work fine because they negotiate the
# version with the server, starting with NFSv4.
# We would normally use "nfsvers=3" here, but this is not understood
# by Debian's initrd (nfsmount from klibc).  So, use the old "v3"
# syntax, which is understood both by the kernel (for initrd-less
# images) and by Debian's initrd.
set nfs_bootargs root=/dev/nfs nfsroot=${nfs_root},v3

set walt_bootargs walt.node.model=${node_model} walt.server.ip=${next-server}

# BOOTIF is set by pxelinux to the MAC address of the interface used to boot,
# and is understood by Debian's initrd scripts.  Use the same syntax as pxelinux
# to be compatible.
set other_bootargs init=${walt_init} panic=30 ip=dhcp BOOTIF=01-${mac:hexhyp}

set bootargs ${nfs_bootargs} ${walt_bootargs} ${other_bootargs}

echo server script

initrd ${initrd_url} || echo No initrd found

# boot
echo Please boot... ${bootargs}
boot ${kernel_url} ${bootargs}

